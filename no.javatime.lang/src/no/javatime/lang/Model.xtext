grammar no.javatime.lang.Model with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

generate model "http://www.javatime.no/lang/Model"

import "http://www.eclipse.org/xtext/xbase/Xtype"
import "http://www.eclipse.org/xtext/xbase/Xbase"
import "http://www.eclipse.org/xtext/common/JavaVMTypes"

Model:
    {Model}
    // importSection=XImportSection?
   	elements += Element*;
 
PackageDeclaration:
   importSection=XImportSection?
  'package' name = QualifiedName '{'
    packageElements += Element*
  '}';
  
Element:
  PackageDeclaration | ModelElementType;

 ModelElementType:
    typeName = ModelElementTypeName modelElementAnnotations+=XAnnotation* 
    name=ValidID ('extends' superType=JvmTypeReference)? 
    ('implements' implements+=JvmTypeReference (',' implements+=JvmTypeReference)*)? '{'
        members+=Member*
    '}';

Member:
    Field | Operation;
 
Field:
   typeName = FieldTypeName? fieldAnnotations+=XAnnotation* memberType=JvmTypeReference name= ValidID ('=' initExp = XExpression)?;


Operation:
    typeName = OperationTypeName operationAnnotations+=XAnnotation* memberType=JvmTypeReference name=ValidID 
        '('(formalParameters+=FullJvmFormalParameter 
        (',' formalParameters+=FullJvmFormalParameter)*)?')'
    body=XBlockExpression;

enum ModelElementTypeName:
	ENDOGENOUS = 'endogenous' | EXOGENOUS = 'exogenous' | STATE = 'state' | TRANSITION = 'transition' 
	| LEVEL='level' | RATE = 'rate' | SYSTEM = 'system' | INPUT='input' | ELEMENT = 'element'; 
 
enum FieldTypeName: FIELD = 'field' | SERIESVALUE = 'seriesValue' | INSERT = 'insert';

enum OperationTypeName:
	OPERATON = 'operation' | ACTION = 'action' | STARTVALUE = 'startValue' | SETSERIESVALUE = 'setSeriesValue' | 
	GETSERIESVALUE = 'getSeriesValue' | START = 'start' | STOP = 'stop';